"macro mpstune for microwave cavity tune"

$action = 'setup'
if ($# > 0) then
  $action = $1
endif

if ($action = 'setup') then
  exists('mpstune','parlib'):$e,$path
  if ($e) then
    "check if MPS is accessible"
    sethw('mps','freq?'):$ok,$frq
    if ($ok = 0) then
      write('error','Cannot access MPS hardware')
      return
    endif
    "save current experiment"
    exists(curexp+'/tutmp','file'):$e
    if ($e < 0.5) then
      tmpsave('tutmp')
    endif
    "get tune parameters"
    rtp($path)
  else
    write('error','parameters for mpstune do not exist')
    return
  endif
  werr=''
  wdone=''
  in='n'
  pad=0
  spin = 'n'
  ss = 0
  bs=1
  full
  rp=0
  lp=0
  ph
  mpsfreq=$frq*1e-6
  sethw('mps','rfsweeppower?'):$ok,$pwr
  if not $ok then
    $pwr=100
  else
    if ($pwr = 0) then
      $pwr=100
    endif
  endif
  tupwr=$pwr/10
  alock='n'
  wshim='n'
  acqmode=''
  saxis
  fn_left=mpsfreq - 1e-3*tunesw/2.0
  fn_right=mpsfreq + 1e-3*tunesw/2.0
  fn_label='GHz'
  sethw('mps','screen 1'):$ok
elseif ($action = 'start') then
  wbs='mpstune(`process`)'
  wexp='mpstune(`reset`)'
  acqmode='mpstune'
  if tunesw < 0.1 then tunesw = 0.1 endif
  au('silent','mpstune')
  clear(2)
  f

elseif ($action = 'reset') then
  acqmode='mpstune'
  wbs='mpstune(`process`)'
  au('silent','mpstune')
  clear(2)
elseif ($action = 'process') then
  ft('noft','fidshim','nods','nopars')
  if (acqstatus[2] = 1) then
    clear(0) vsadj
    mfShowAxis=1
    "draw center line"
    write('line3','1 LINE %g -20 %g %g  |%%val%%| 0 0 256 2 0 1 integral 0 - - - 0.00',mpsfreq, mpsfreq, wc2max):$cmd
    aspAnno('clear')
    aspAnno('paste',$cmd)
  endif
  $cr=crmode
  ds
  if ($cr = 'b') then
    ds('toggle')
  endif

elseif ($action = 'setfreq') then
  if ($2 <> mpsfreq) then
    if (acqmode='mpstune') then
      mpsfreq=$2
      setvalue('mpsfreq',$2,'processed')
      acqmode=''
      wbs=''
      halt(' ')
      sleep(0.2)
    else
      setmps('freq',$2)
    endif
    fn_left=mpsfreq - 1e-3*tunesw/2.0
    fn_right=mpsfreq + 1e-3*tunesw/2.0
  endif

elseif ($action = 'movecf') then
  if (cr <> mpsfreq) then
    if (acqmode='mpstune') then
      mpsfreq=cr
      setvalue('mpsfreq',cr,'processed')
      acqmode=''
      wbs=''
      halt(' ')
      sleep(0.2)
    else
      setmps('freq',cr)
    endif
    fn_left=mpsfreq - 1e-3*tunesw/2.0
    fn_right=mpsfreq + 1e-3*tunesw/2.0
  endif

elseif ($action = 'setspan') then
  if ($2 <> tunesw) then
    tunesw=$2
    if (acqmode='mpstune') then
      setvalue('tunesw',$2,'processed')
      acqmode=''
      wbs=''
      halt(' ')
      sleep(0.2)
    endif
    fn_left=mpsfreq - 1e-3*tunesw/2.0
    fn_right=mpsfreq + 1e-3*tunesw/2.0
  endif

elseif ($action = 'setpwr') then
  if ($2 <> tupwr) then
    tupwr=$2
    if (acqmode='mpstune') then
      setvalue('tupwr',$2,'processed')
      acqmode=''
      wbs=''
      halt(' ')
      sleep(0.2)
    else
      setmps('tunepwr',$2)
    endif
  endif

elseif ($action = 'np') then
  if ($2 <> np) then
    np=$2
    if (acqmode='mpstune') then
      setvalue('np',$2,'processed')
      acqmode=''
      wbs=''
      halt(' ')
      sleep(0.2)
    endif
  endif

elseif ($action = 'stop') then
  if (acqmode='mpstune') then
    aa('Tuning Complete')
  endif
  acqmode=''

elseif ($action = 'quit') then
  aspAnno('clear')
  clear(2)
  if (acqmode='mpstune') then
    aa('Tuning Complete')
  endif
  sethw('mps','screen 0'):$ok
  acqmode=''
  tmpreturn('tutmp')
  exists('acqstatus','parameter'):$e
  if (not $e) then
    // This keeps cpreact from complaining
    create('acqstatus','integer')
    setprotect('acqstatus','on',256)
  endif
  acqstatus=16,102
  newdg dg
endif
