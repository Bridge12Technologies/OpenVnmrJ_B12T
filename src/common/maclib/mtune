"macro mtune"
" modified by Karl rieger to include B12SCNControl() vnmrj macro to control B12SCN "

$action = 'setup'
if ($# > 0) then
  $action = $1
endif

if ($action = 'setup') or ($action = 'prep') then

  "Read nuclei present in probe file"
  $out=userdir+'/persistence/mtune_nuc'
  getProbePath:$e,$probe
  if ($e=0) then $probe='none' endif
  write('reset',$out)

  if $probe='none' then     "For imaging, no probe file set"
    write('file',$out,'"H1" H1')
    write('file',$out,'"H2" H2')
    write('file',$out,'"C13" C13')
    write('file',$out,'"F19" F19')
    write('file',$out,'"P31" P31')
    write('file',$out,'"Na23" Na23')
    write('file',$out,'"Xe129" Xe129')
  else  "Read nuclei present in probe file"
    lookup('file',$probe)
    repeat
      lookup('readline'):$string,$nret
      $b=''
      strstr($string,'dmf '):$a,$b
      if ($b<>'') and ($b<>'lkgn_') and ($b<>'lkpwr_') then
        write('file',$out,'"'+$b+'" '+$b)
      endif
    until ($nret=0)
  endif
  if $action='prep' then return endif
endif

"save current experiment"

if ($action = 'setup') then
  exists('mtune','parlib'):$e,$path
  if ($e) then
    "save current experiment"
    exists(curexp+'/tutmp','file'):$e
    if ($e < 0.5) then
      tmpsave('tutmp')
    endif
    "get tune parameters"
    rtp($path)
  else
    write('error','parameters for mtune do not exist')
    return
  endif

  werr=''
  wdone=''
  in='n'
  pad=0
  spin = 'n'
  ss = 0
  bs=1
  full
  av
  alock='n'
  wshim='n'
  setlimit('vp',1e5,-1e5,0)
  if tpwrf > tpwrfmax then
    tpwrf = tpwrfmax
  endif
  if np > 1024 then
    np = 1024
  endif
  acqmode=''
  saxis
  fn_left=sfrq - 1e-6*tunesw/2.0
  fn_right=sfrq + 1e-6*tunesw/2.0
  fn_label='MHz'
  mtune('bgname'):$e
  if ($e) then
    disp1=2
    disp2=1
    disp3=1
  else
    disp1=2
    disp2=0
    disp3=0
  endif
elseif ($action = 'start') then

  "add B12SCNControl here and set power automatically to 0.01"
  mtune('setpwr',1)
  write('line3','Set power to 1!')
  B12SCNControl('tune 1','silent')
  sleep(0.05)
  $scnMode=''
  B12SCNControl('tune?','silent'):$scnMode
  if ( $scnMode <> '1' ) then
    write('error','Could not set Bridge12SCN in tune mode, aborting! Is the USB connected?')
    return
  endif
  wbs='mtune(`process`)'
  wexp='mtune(`reset`)'
  acqmode='mtune'
  if tunesw < 0.1 then tunesw = 0.1 endif
  $mps=mps
  mps='manual'
  au('silent','tune')
  mps=$mps
  clear(2)
  f
elseif ($action = 'reset') then
  sleep(2)
  wbs='mtune(`process`)'
  $mps=mps
  mps='manual'
  au('silent','tune')
  mps=$mps
  clear(2)
elseif ($action = 'process') then
  ft('noft','fidshim','nods','nopars')
  if (acqstatus[2] = 1) then
    clear(0) vsadj
    mfShowAxis=1
    "draw center line"
    write('line3','1 LINE %g -20 %g %g  |%%val%%| 0 0 256 2 0 1 integral 0 - - - 0.00',sfrq, sfrq, wc2max):$cmd
    aspAnno('clear')
    aspAnno('paste',$cmd)
  endif
  $cr=crmode
  mspec('clear')
  $num=0
  if (disp3=2) then
     aipLoadSpec(bgfile,'bg')
     if (disp1 = 2) then
        $num=$num+1
        mspec($num,'SPEC:1',0,'modelSpec')
     endif
     if (disp2 = 2) then
        $num=$num+1
        mspec($num,'bg:1',1,'modelSpec')
     endif
     $num=$num+1
     mspec($num,'SPEC:1-bg:1',0,'spectrum')
  else
     if (disp1 = 2) then
        $num=$num+1
        mspec($num,'SPEC:1',0,'spectrum')
     endif
     if (disp2 = 2) then
        $num=$num+1
        mspec($num,'bg:1',1,'modelSpec')
     endif
  endif
  if ($num = 0) then
    disp1=2
  endif
  ds
  if ($cr = 'b') then
    ds('toggle')
  endif

elseif ($action = 'setfreq') then
  sfrq=$2 setvalue('sfrq',sfrq,'processed')
  if (acqmode='mtune') then
    wbs=''
    halt(' ')
  endif
  fn_left=sfrq - 1e-6*tunesw/2.0
  fn_right=sfrq + 1e-6*tunesw/2.0
  procdim=0
  mtune('bgname'):$e
  if (not $e) then
    disp2=0
    disp3=0
  else
    if (disp2=0) then disp2=1 endif
    if (disp3=0) then disp3=1 endif
  endif
elseif ($action = 'setspan') then
  tunesw=$2
  if (acqmode='mtune') then
    wbs=''
    halt(' ')
  endif
  fn_left=sfrq - 1e-6*tunesw/2.0
  fn_right=sfrq + 1e-6*tunesw/2.0
  procdim=0
  mtune('bgname'):$e
  if (not $e) then
    disp2=0
    disp3=0
  else
    if (disp2=0) then disp2=1 endif
    if (disp3=0) then disp3=1 endif
  endif
elseif ($action = 'setpwr') then
  tpwrf=$2
  if tpwrf > tpwrfmax then
    tpwrf = tpwrfmax
  endif
  if (acqmode='mtune') then
    wbs=''
    halt(' ')
  endif
elseif ($action = 'getfreq') then
    return(sfrq)
elseif ($action = 'getnuc') then
    return(tn)
elseif ($action = 'setnuc') then
  tn=$2 setvalue('tn',tn,'processed')
  if (acqmode='mtune') then
    wbs=''
    halt(' ')
  endif
  fn_left=sfrq - 1e-6*tunesw/2.0
  fn_right=sfrq + 1e-6*tunesw/2.0
  procdim=0
  mtune('bgname'):$e
  if (not $e) then
    disp2=0
    disp3=0
  else
    if (disp2=0) then disp2=1 endif
    if (disp3=0) then disp3=1 endif
  endif
elseif ($action = 'defProbe') then
  $defined=$3
  if (probe <> '') then
     exists(systemdir+'/probes/'+probe+'/'+probe,'file'):$e
     if ( not $e) then
        exists(userdir+'/probes/'+probe+'/'+probe,'file'):$e
     endif
     if ($e) then $defined=$2 else $defined=$3 endif
  endif
  return($defined)

elseif ($action = 'bgname') then
  write('line3','tune_%1.2f_%1.1f.fdf',sfrq,tunesw*1e-6):$name
  $file=systemdir+'/probes/'+probe+'/'+probe
  exists($file,'file','rw'):$e
  if (not $e) then
    $file=userdir+'/probes/'+probe+'/'+$name
    exists($file,'file'):$e
  else
    $file=systemdir+'/probes/'+probe+'/'+$name
    exists($file,'file','rw'):$e
  endif
  bgfile=$file
  return($e)

elseif ($action = 'savebg') then
  mtune('bgname'):$e
  if ($e) then
    delete(bgfile,''):$e
  endif
  savefdfspec(bgfile):$e
  write('line3','Background scan saved for frequency %1.2f and span %1.1f',
         sfrq, tunesw*1e-6)
  if (disp2=0) then disp2=1 endif
  if (disp3=0) then disp3=1 endif

elseif ($action = 'stop') then
  if (acqmode='mtune') then
    aa('Tuning Complete')
  endif
  acqmode=''

elseif ($action = 'quit') then
  if (acqmode='mtune') then
    aa('Tuning Complete')
  endif
  mspec('clear')
  aspAnno('clear')
  clear(2)
  acqmode=''
  tmpreturn('tutmp')
  exists('acqstatus','parameter'):$e
  if (not $e) then
    // This keeps cpreact from complaining
    create('acqstatus','integer')
    setprotect('acqstatus','on',256)
  endif
  acqstatus=16,102
  newdg dg

  sleep(0.05)
  $scnMode=''
  B12SCNControl('tune 0','silent'):$scnMode
  sleep(0.05)
  B12SCNControl('state?','silent'):$scnMode
  sleep(0.05)
  if ( $scnMode = 'tune mode' ) then
    write('error','Could not quit Bridge12SCN tune mode! Are you sure that the B12SCN is connected?')
    return(1)
  endif
  write('line3','Ending tune mode, going back to state %s',$scnMode)

endif
